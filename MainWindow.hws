@INCLUDE "RQWeatherClient/RQWeatherClient.hws"
@INCLUDE "RQWeatherClient/HTTPStatus.hws"
@INCLUDE "RQWeatherClient/DTO/GeoDTO.hws"
@INCLUDE "StringLib/StringUtils.hws"
@INCLUDE "AppUtil.hws"
@INCLUDE "Preferences.hws"
@INCLUDE "PrefsWindow.hws"
@INCLUDE "IconCacheLib/IconCache.hws"

; Window message constants
Const #MSG_CLASS_BUTTON = "Button"
Const #MSG_CLASS_MNUITM = "Menuitem"
Const #MSG_ATTR_PRESSED = "Pressed"
Const #MSG_ATTR_SELECTED = "Selected"
Const #MSG_TE_LOG = "teLog"

; Buttons
Const #BTN_SEL_LOC = "btnSelectLoc"
Const #BTN_GET_WEATHER = "btnGetWeather"
Const #BTN_FETCH = "btnFetch"

; Menus
Const #MNU_ABOUT = "mnuAbout"
Const #MNU_QUIT = "mnuQuit"
Const #MNU_PREFS = "mnuPreferences"

; Text input fields
Const #TE_CITY = "teCity"
Const #TE_STATE_CODE = "teStateCode"
Const #TE_COUNTRY_CODE = "teCountryCode"

; Labels
Const #LBL_LATITUDE = "lblLatitude"
Const #LBL_LONGITUDE = "lblLongitude"
Const #LBL_RIGHT_NOW = "lblRightNow"
Const #LBL_CURRENT_TEMP = "lblTemp"
Const #LBL_HIGH_TEMP = "lblHighTemp"
Const #LBL_LOW_TEMP = "lblLowTemp"
Const #LBL_HUMIDITY = "lblHumidity"
Const #LBL_CLOUDINESS = "lblCloudiness"
Const #LBL_RAIN = "lblRain"
Const #LBL_SUNRISE = "lblSunrise"
Const #LBL_FEELS_LIKE = "lblFeelsLike"
Const #LBL_PRESSURE = "lblPressure"
Const #LBL_VISIBILITY = "lblVisibility"
Const #LBL_WIND = "lblWind"
Const #LBL_SNOW = "lblSnow"
Const #LBL_SUNSET = "lblSunset"
Const #LBL_DAY_1_COND = "lblDay1Cond"
Const #LBL_DAY_1_TEMPS = "lblDay1Temps"
Const #LBL_DAY_2_COND = "lblDay2Cond"
Const #LBL_DAY_2_TEMPS = "lblDay2Temps"
Const #LBL_DAY_3_COND = "lblDay3Cond"
Const #LBL_DAY_3_TEMPS = "lblDay3Temps"
Const #LBL_DAY_4_COND = "lblDay4Cond"
Const #LBL_DAY_4_TEMPS = "lblDay4Temps"
Const #LBL_DAY_5_COND = "lblDay5Cond"
Const #LBL_DAY_5_TEMPS = "lblDay5Temps"

; Progress bars
Const #PB_PROGRESS = "pbProgress"

; Image boxes
Const #IMG_COND = "imgCondition"
Const #IMG_DAY_1_COND = "imgDay1Cond"
Const #IMG_DAY_2_COND = "imgDay2Cond"
Const #IMG_DAY_3_COND = "imgDay3Cond"
Const #IMG_DAY_4_COND = "imgDay4Cond"
Const #IMG_DAY_5_COND = "imgDay5Cond"

; File IDs
Const #FILE_WINDOW_DEF = "MainWindow.xml"
Const #FILE_BRUSH_DEF_ICON = "icons/weather.png"
Const #ID_WINDOW_DEF = 1
Const #ID_BRUSH_WEATHER_COND = 2

Global pCurrentLocation = Nil
Global pDownloading = False
Global pImageIndex = 0
Global pCurrentFilename = Nil

Function p_MainWindow_Log(text)
    Local eolIndex = 0
    Local currentText = moai.Get(#MSG_TE_LOG, "Text")
    Local len = StringUtils.StringLength(currentText)
    If len > 5000
        eolIndex = StringUtils.IndexOf(currentText, "\n", 0)
        currentText = StringUtils.Substring(currentText, eolIndex, len - eolIndex)
    EndIf

    Local timestamp = GetDate() .. " " .. GetTime()
    currentText = currentText .. "\n" .. timestamp .. " - " .. text .. "\n"
    moai.Set(#MSG_TE_LOG, "Text", currentText)

    Local pos = StringUtils.StringLength(currentText) - 1
    moai.Set(#MSG_TE_LOG, "CursorPos", pos)
EndFunction

Function p_MainWindow_SetProgressBar(value, max)
	If IsNil(value) Then value = 0
	If IsNil(max) Then max = 0
	moai.Set(#PB_PROGRESS, "Max", max)
	moai.Set(#PB_PROGRESS, "Level", value)
EndFunction

Function p_MainWindow_Reload()
	Preferences.Load()
	RQWeatherClient.Address = Preferences.ApiHostAddress
	RQWeatherClient.Port = Preferences.ApiHostPort
	If IsNil(pCurrentLocation)
		If Preferences.LocationLatitude <> 0 And Preferences.LocationLongitude <> 0
			pCurrentLocation = GeoDTO:New()
			pCurrentLocation.Latitude = Preferences.LocationLatitude
			pCurrentLocation.Longitude = Preferences.LocationLongitude
			pCurrentLocation.Name = Preferences.LocationCity
			pCurrentLocation.State = Preferences.LocationState
			pCurrentLocation.Country = Preferences.LocationCountryCode
		Else
			Return ()
		EndIf
	EndIf
	
	moai.Set(#TE_CITY, "Text", pCurrentLocation.Name)
	moai.Set(#TE_STATE_CODE, "Text", pCurrentLocation.State)
	moai.Set(#TE_COUNTRY_CODE, "Text", pCurrentLocation.Country)
	moai.Set(#LBL_LATITUDE, "Text", ToString(pCurrentLocation.Latitude))
	moai.Set(#LBL_LONGITUDE, "Text", ToString(pCurrentLocation.Longitude))
	p_MainWindow_SetProgressBar(0, 100)
EndFunction

Function p_MainWindow_SetCurrentLocation(location)
	pCurrentLocation = location
	Preferences.LocationLatitude = pCurrentLocation.Latitude
	Preferences.LocationLongitude = pCurrentLocation.Longitude
	Preferences.LocationCity = pCurrentLocation.Name
	Preferences.LocationState = pCurrentLocation.State
	Preferences.LocationCountryCode = pCurrentLocation.Country
	Preferences.Save()
	p_MainWindow_Reload()	
EndFunction

Function p_MainWindow_PopulateImage(expectedFileName, fileNameActual)
	If expectedFileName = pCurrentFilename
		Local brushId = LoadBrush(Nil, fileNameActual)
		Switch pImageIndex
		Case 0:
			moai.Set(#IMG_COND, "Brush", brushId)
		Case 1:
			moai.Set(#IMG_DAY_1_COND, "Brush", brushId)
		Case 2:
			moai.Set(#IMG_DAY_2_COND, "Brush", brushId)
		Case 3:
			moai.Set(#IMG_DAY_3_COND, "Brush", brushId)
		Case 4:
			moai.Set(#IMG_DAY_4_COND, "Brush", brushId)
		Case 5:
			moai.Set(#IMG_DAY_5_COND, "Brush", brushId)
		EndSwitch
	EndIf
EndFunction

Function p_MainWindow_LoadImageFromCacheOrSource(fileUri)
	Local nameParts = StringUtils.StringSplit(fileUri, "/")
	pCurrentFilename = GetTime(nameParts, TableItems(nameParts) - 1)

	Local imageFile = IconCache.GetIcon(pCurrentFilename)
	If IsNil(imageFile)
		pDownloading = True
		p_MainWindow_SetProgressBar(0, 100)
		p_MainWindow_Log("Downloading image: " .. fileUri)
		RQWeatherClient.GetFile(fileUri, IconCache.GetIconSavePath(pCurrentFilename))
	Else
		p_MainWindow_Log("Loading image from cache: " .. imageFile)
		p_MainWindow_PopulateImage(pCurrentFilename, imageFile)
	EndIf
EndFunction

Function p_MainWindow_GetCurrentWeather()
	If IsNil(pCurrentLocation)
		Return ()
	EndIf
	
	RQWeatherClient.Init()
	Local lat = pCurrentLocation.Latitude
	Local lon = pCurrentLocation.Longitude

	; Fetch current weather data and check response status
	Local currentWeather = RQWeatherClient.GetCurrentWeather(lat, lon)
	If currentWeather.code = HTTPStatus.STATUS_200_OK
		; Get current condition
		Local condition = currentWeather.weather[0]
		pImageIndex = 0
		p_MainWindow_LoadImageFromCacheOrSource(condition.icon)

		; Adjust timestamp for timezone
		Local timestamp = currentWeather.timestamp
		Local tstamp = DateToTimestamp(MakeDate(timestamp))
		tstamp = tstamp + currentWeather.timezone
		timestamp = TimestampToDate(tstamp)
		Local tstampStr = StringUtils.DateToTimestamp(timestamp)

		; Display current conditions
		moai.Set(#LBL_RIGHT_NOW, "Text", condition.description " .. as of " .. tstampStr)
		moai.Set(#LBL_CURRENT_TEMP, "Text", ToString(currentWeather.main.tempMin) .. "F")
		moai.Set(#LBL_FEELS_LIKE, "Text", ToString(currentWeather.main.feelsLike) .. "F")
		moai.Set(#LBL_HIGH_TEMP, "Text", ToString(currentWeather.main.tempMax) .. "F")
		moai.Set(#LBL_LOW_TEMP, "Text", ToString(currentWeather.main.tempMin) .. "F")
		moai.Set(#LBL_PRESSURE, "Text", ToString(currentWeather.main.pressure) .. "F")
		moai.Set(#LBL_HUMIDITY, "Text", ToString(currentWeather.main.humidity) .. "%")
		moai.Set(#LBL_VISIBILITY, "Text", ToString(currentWeather.visibility * 0.0006211371) .. " Miles")

		; Display cloud data if present
		If GetType(currentWeather.clouds) = #TABLE
			moai.Set(#LBL_CLOUDINESS, "Text", ToString(currentWeather.clouds.all) .. "%")
		EndIf

		; Display wind data if present
		If GetType(currentWeather.wind) = #TABLE
			Local windSpeed = currentWeather.wind.speed
			Local direction = currentWeather.wind:ToWindDirection()
			Local gust = currentWeather.wind.gust
			moai.Set(#LBL_WIND, "Text", ToString(windSpeed) .. "MPH " .. direction .. ", Gust: " .. ToString(gust) .. "MPH")
		EndIf

		Local oneHour = 0
		Local threeHour = 0

		; Display rain data if present
		If GetType(currentWeather.rain) = #TABLE
			oneHour = currentWeather.rain.oneHour / 25.4
			threeHour = currentWeather.rain.threeHour / 25.4
			moai.Set(#LBL_RAIN, "Text", "Last Hour: " .. ToString(Round(oneHour)) .. ", Last 3 Hours: " .. ToString(Round(threeHour)))	
		EndIf

		; Display snow data if present
		If GetType(currentWeather.snow) = #TABLE
			oneHour = currentWeather.snow.oneHour / 25.4
			threeHour = currentWeather.snow.threeHour / 25.4
			moai.Set(#LBL_SNOW, "Text", "Last Hour: " .. ToString(Round(oneHour)) .. ", Last 3 Hours: " .. ToString(Round(threeHour)))
		EndIf

		; Compute and display sunrise timestamp adjusted for timezone
		timestamp = currentWeather.system.sunrise
		tstamp = DateToTimestamp(MakeDate(timestamp))
		tstamp = tstamp + currentWeather.timezone
		timestamp = TimestampToDate(tstamp)
		Local sunrise = StringUtils.DateToTimestamp(timestamp)
		moai.Set(#LBL_SUNRISE, "Text", sunrise)

		; Compute and display sunset timestamp adjusted for timezone
		timestamp = currentWeather.system.sunset
		tstamp = DateToTimestamp(MakeDate(timestamp))
		tstamp = tstamp + currentWeather.timezone
		timestamp = TimestampToDate(tstamp)
		Local sunset = StringUtils.DateToTimestamp(timestamp)
		moai.Set(#LBL_SUNSET, "Text", sunset)
	EndIf

	RQWeatherClient.Close()
EndFunction

Function p_MainWindow_ProcessGUI(message)
    DebugPrint("DEBUG: Event class: " .. message.Class)
    DebugPrint("DEBUG: Event attrib: " .. message.Attribute)
    DebugPrint("DEBUG: Event ID: " .. message.ID)
    Switch message.Class
    Case #MSG_CLASS_BUTTON:
        Switch message.Attribute
        Case #MSG_ATTR_PRESSED:
            Switch message.ID
            Case #BTN_SEL_LOC:
				DebugPrint("Select location pressed!")
            Case #BTN_GET_WEATHER:

            Case #BTN_FETCH:
			
			Case #BTN_PREFS_CANCEL:
				p_PrefsWindow_Close()
			Case #BTN_PREFS_SAVE:
				p_PrefsWindow_SaveAndClose()
            EndSwitch
        EndSwitch
    Case #MSG_CLASS_MNUITM:
        Switch message.Attribute
		Case #MSG_ATTR_SELECTED:
			Switch message.ID
        	Case #MNU_ABOUT:
            	moai.Request("About", "MOWeather\n" ..
                	"Â© 2025 by Cyrus Brunner")
        	Case #MNU_QUIT:
            	End
			Case #MNU_PREFS:
				p_PrefsWindow_Show()
        	EndSwitch
		EndSwitch
    EndSwitch
EndFunction

Function p_MainWindow_ReceiveProgress(totalBytes, bytesReceived)
	pDownloading = True
	Local value = (bytesReceived * 100) / totalBytes
	p_MainWindow_SetProgressBar(value, totalBytes)
EndFunction

Function p_MainWindow_ProcessingRequest()
	p_MainWindow_Log("Beginning API request")
EndFunction

Function p_MainWindow_ProcessingComplete()
	p_MainWindow_Log("API request complete")
EndFunction

Function p_MainWindow_DownloadComplete(url, httpStatus, filePath)
	p_MainWindow_SetProgressBar(100, 100)
	If httpStatus = HTTPStatus.STATUS_200_OK And Not(IsNil(filePath))
		p_MainWindow_PopulateImage(FilePart(filePath), filePath)
	EndIf
	
	pDownloading = False
EndFunction

Function p_MainWindow_Connected()
	p_MainWindow_Log("Client connected")
EndFunction

Function p_MainWindow_ApiSuccess(errorCode, desc)
	p_MainWindow_Log("Request result: [" .. ToString(errorCode) .. "] " .. desc)
EndFunction

Function p_MainWindow_ApiError(errorCode, desc, inUrl, inResponse)
	p_MainWindow_Log("API Error: [" .. ToString(errorCode) .. "] " .. desc .. " (URL: " .. inUrl .. ")")
	p_MainWindow_Log("Error response: " .. inResponse)
EndFunction

Function p_MainWindow_Init()
	; Load GUI definition file
	@FILE #ID_WINDOW_DEF, #FILE_WINDOW_DEF

	; Load default weather icon
	LoadBrush(#ID_BRUSH_WEATHER_COND, #FILE_BRUSH_DEF_ICON)

	moai.CreateApp(ReadString(#ID_WINDOW_DEF))
	InstallEventHandler({RapaGUI=p_MainWindow_ProcessGUI})

	IconCache.Init()
	IconCache.ClearCache()
	
	RQWeatherClient.OnProcessingRequest = p_MainWindow_ProcessingRequest
	RQWeatherClient.OnProcessingComplete = p_MainWindow_ProcessingComplete
	RQWeatherClient.OnXferProgress = p_MainWindow_ReceiveProgress
	RQWeatherClient.OnDownloadComplete = p_MainWindow_DownloadComplete
	RQWeatherClient.OnConnected = p_MainWindow_Connected
	RQWeatherClient.OnApiSuccess = p_MainWindow_ApiSuccess
	RQWeatherClient.OnApiError = p_MainWindow_ApiError
	

	p_MainWindow_Reload()
	p_MainWindow_Log("Ready.")
EndFunction